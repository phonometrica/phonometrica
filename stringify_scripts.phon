#!/usr/bin/env phonometrica

local blacklist = []
local root = get_full_path('.')
local stdlib = join_path(root, 'std')
local outdir = join_path(join_path(root, 'phon'), 'include')
local dirs = list_directory(stdlib, false)

foreach name in dirs do
	if not ends_with(name, '.phon') or contains(blacklist, name) then
		continue
	end
		
	local f = join_path(stdlib, name)
	local content = []
	local multiline_comment = false
	local previous_empty = false # don't include multiple empty lines
	
	# Get lines, stripping comments
	foreach ln in open(f) do
		trim(ln)
		simple_comment = false

		if starts_with(ln, '#') then
			simple_comment = true
		elsif starts_with(ln, '!*') then
			multiline_comment = true
		elsif ends_with(ln, '*!') then
			simple_comment = true # make sure the line is not included
			multiline_comment = false
		end
		
		local empty = is_empty(ln)
				
		if not simple_comment and not multiline_comment and not (empty and previous_empty) then
			append(content, ln)
		end
			
		previous_empty = empty
	end
			
	local base = strip_extension(name)
	local outname = join_path(outdir, base & '_phon.hpp')
	local include = to_upper(base) & '_PHON_SCRIPT_INCLUDE'

	local outfile = open(outname, 'w')
	write(outfile, '#ifndef ' & include & '\n')
	write(outfile, '#define ' & include & '\n\n')
	write(outfile, 'static const char *' & base & '_script = R"_(')
	write_lines(outfile, content)
	write(outfile, ')_";\n\n')
	write(outfile, '#endif /* ' & include & ' */')
	close(outfile)
end
	
